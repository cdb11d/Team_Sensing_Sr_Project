/*****************************************************
 * This UartEvent uses the "Read Bytes Until" from 
 * user duff2013's "UartEvent" event where it will
 * fire the RX event handler when a termination
 * character is detected or the buffer is full.
 *
 *****************************************************/
#include <UartEvent.h>

Uart1Event Event1;
volatile bool print_flag = false;// flag to indicate rx buffer full

const uint16_t BUFSIZE = Event1.rxBufferSize;// size of internal buffer
char buffer[BUFSIZE + 1]; // user variable to hold incoming data

void setup() {
    pinMode(LED_BUILTIN, OUTPUT);
    Serial.begin(0);
    while (!Serial);
    delay(100);
    //--------------------------Uart1Event Configuration--------------------------------
    Event1.loopBack = true;              // internal loopback set / "default = false"
    Event1.txEventHandler = tx1Event;    // event handler Serial1 TX
    Event1.rxEventHandler = rx1Event;    // event handler Serial1 RX
    Event1.rxTermCharacterTrigger = '\n';// this termination character will fire the RX event handler
    Event1.begin(9600);                  // start serial port
    //----------------------------------------------------------------------------------
    Serial.println("RX event will fire when termination character is detected");
}

void loop() {
    if (Serial.available()) {     // if serial data is available
        char c = Serial.read();   // read and store as 'c'
        Event1.print(c);          // store 'c' in Event1
    }
    
    if (print_flag) {             // if rx buffer is full
        Serial.print(buffer);     // print on serial monitor
        print_flag = false;       // reset rx buffer flag
    }
}

//--------------------------------------Serial1 Events--------------------------------
void tx1Event(void) {
    // TX Event function will fire when it is finished sending a packet
}

void rx1Event(void) {
    // RX Event function prints the buffer when it is full
    digitalWrite(LED_BUILTIN, !digitalRead(LED_BUILTIN));     // toggle built-in LED
    int x = 0;
    while (Event1.available()) buffer[x++] = Event1.read();   // fill rx buffer with Event1 data
    buffer[x] = 0;                                            // fill last bit in the rx buffer
    print_flag = true;                                        // trigger rx buffer flag
}
